buildscript {
    ext {
        versions = [
            "protobuf":   '3.6.1',
            "grpc":       '1.17.1',
            "coroutines": '1.0.1',
            "krotoplus":  '0.2.2-SNAPSHOT'
        ]
    }
}

plugins{
    id 'idea'
    id 'com.google.protobuf' version '0.8.7'
    id "org.jetbrains.kotlin.jvm" version "1.3.11"
}


compileKotlin {
    kotlinOptions{
        jvmTarget = "1.8"
        freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
    }
}

repositories {
    jcenter()
    if(versions.krotoplus.endsWith("SNAPSHOT")) {
        flatDir dirs: "${rootDir}/../kroto-plus-coroutines/build/libs/"
        flatDir dirs: "${rootDir}/../protoc-gen-kroto-plus/build/libs/"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}"
    implementation "com.github.marcoferrer.krotoplus:kroto-plus-coroutines:${versions.krotoplus}"

    implementation "com.google.protobuf:protobuf-java:${versions.protobuf}"
    implementation "io.grpc:grpc-protobuf:${versions.grpc}"
    implementation "io.grpc:grpc-stub:${versions.grpc}"
    implementation "io.grpc:grpc-netty:${versions.grpc}"
}



protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${versions.protobuf}"}

    generatedFilesBaseDir = "$projectDir/src/"

    //noinspection GroovyAssignabilityCheck
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${versions.grpc}" }
        kroto {
            artifact = "com.github.marcoferrer.krotoplus:protoc-gen-kroto-plus:${versions.krotoplus}:jvm8@jar"
        }
    }

    generateProtoTasks {
        def krotoConfig = file("krotoPlusConfig.asciipb")

        all().each{ task ->
            // Adding the config file to the task inputs lets UP-TO-DATE checks
            // include changes to configuration
            task.inputs.files krotoConfig

            task.builtins {
                java{ outputSubDir = 'generated' }
            }
            task.plugins {
                grpc { outputSubDir = "generated" }
                kroto {
                    outputSubDir = "generated"
                    option "ConfigPath=$krotoConfig"
                }
            }
        }
    }
}
